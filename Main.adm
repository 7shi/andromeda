printfln("Andromeda ver.%s", Root.VERSION);
var start = DateTime.Now;

var tests = Directory.GetFiles("not-Tests");
for (int i = 0; i < tests.Length; i++)
{
    var adm = tests[i];
    if (Path.GetExtension(adm) == ".adm")
        compile(adm);
}

println();
var end = DateTime.Now;
printfln("総所要時間: %dms", end.Sub(start).TotalMilliseconds);

{ _Finish _finish; }

function compile(src : string)
{
    var output = Path.ChangeExtension(src, ".exe");
    if (File.Exists(output)) return;

    println();
    var s = DateTime.Now;

    var root = new Root;
    readDir(root, "lib-Core");
    //readDir(root, "lib-System");
    parse(root, src);

    printfln("コンパイルしています...");
    var module = new Module;
    module.Specific.SubSystem = IMAGE_SUBSYSTEM.WINDOWS_CUI;
    var codes = OpModule.Create(module);
    root.AddCodes(codes);
    module.Text.OpCodes = codes.ToArray();

    printfln("リンクしています...");
    module.Link(output);
    printfln("出力しました: %s", output);

    var e = DateTime.Now;
    printfln("所要時間: %dms", e.Sub(s).TotalMilliseconds);
}

function parse(root : Root, src : string)
{
    var src2 = src.Replace("\\", "/");
    printfln("パースしています: %s", src2);
    var sr = new StreamReader;
    if (sr.Open(src))
    {
        root.ReadText(src2, sr.ReadToEnd());
        sr.Close();
    }
    else
        raise("ファイルが開けません。");
}

function readDir(root : Root, dir : string)
{
    var dirs = Directory.GetDirectories(dir);
    for (int i = 0; i < dirs.Length; i++)
        readDir(root, dirs[i]);
    var files = Directory.GetFiles(dir);
    for (int i = 0; i < files.Length; i++)
    {
        var file = files[i];
        if (Path.GetExtension(file).ToLower() == ".adm")
            parse(root, file);
    }
}
