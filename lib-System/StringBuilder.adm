class StringBuilder
{
	var Ptr : char[];
	int Length, Capacity;
	
	function ctor()
	{
		Capacity = 16;
		Ptr = new char[Capacity];
		Length = 0;
	}
	
	function Clear()
	{
		Length = 0;
	}
	
	function ToString() : string
	{
		var ret = string.Alloc(Length);
		__movsw(ret, Ptr, Length);
		return ret;
	}
	
	function AppendWithLength(s, length)
	{
		var cap = Capacity;
		var size = Length + length - 1;
		while (size >= Capacity)
			Capacity += Capacity;
		if (Capacity != cap)
		{
			var tmp = new char[Capacity];
			__movsw(tmp, Ptr, Length);
			Ptr = tmp;
		}
		__movsw(addrof Ptr[Length], s, length);
		Length += length;
	}
	
	function Append(format : string, args : params)
	{
		var s = string.VFormat(format, args);
		AppendWithLength(s, s.Length);
	}
	
	function AppendLine(format : string, args : params)
	{
		var s = string.VFormat(format, args);
		AppendWithLength(s, s.Length);
		AppendLineOnly();
	}
	
	function AppendLineOnly()
	{
		Append("\r\n");
	}
	
	function AppendChar(ch)
	{
		AppendWithLength(addrof ch, 1);
	}
}
