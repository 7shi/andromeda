class StringBuilder
{
	int Length = 0, Capacity = 16;
	var buffer = new char[Capacity];
	
	function Clear
	{
		Length = 0;
	}
	
	function ToString
	{
		var ret = string.Alloc(Length);
		__movsw(ret, buffer, Length);
		return ret;
	}
	
	function AppendWithLength(s : char*, length)
	{
		if (length == 0) return;
		var cap = Capacity;
		var size = Length + length - 1;
		while (size >= Capacity)
		{
			Capacity += Capacity;
			Capacity += Capacity;
		}
		if (Capacity != cap)
		{
			var tmp = new char[Capacity];
			__movsw(tmp, buffer, Length);
			buffer = tmp;
		}
		__movsw(addrof buffer[Length], s, length);
		Length += length;
	}
	
	function Append(format : string, args : params)
	{
		var s = string.VFormat(format, args);
		AppendWithLength(s, s.Length);
	}
	
	function AppendLine(format : string, args : params)
	{
		var s = string.VFormat(format, args);
		AppendWithLength(s, s.Length);
		AppendLineOnly();
	}
	
	function AppendLineOnly()
	{
		Append(Environment.NewLine);
	}
	
	function AppendChar(ch : char)
	{
		AppendWithLength(addrof ch, 1);
	}
}
